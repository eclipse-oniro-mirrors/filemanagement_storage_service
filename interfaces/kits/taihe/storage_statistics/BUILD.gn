# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("//foundation/filemanagement/storage_service/storage_service_aafwk.gni")

subsystem_name = "filemanagement"
part_name = "storage_service"
taihe_generated_file_path = "$taihe_file_path/out/$subsystem_name/$part_name/storage_statistics"

copy_taihe_idl("copy_taihe") {
  sources = [
    "${storage_service_path}/interfaces/kits/taihe/storage_statistics/idl/ohos.file.storageStatistics.taihe"
    ]
}

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  deps = [ ":copy_taihe" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.file.storageStatistics.ani.cpp",
    "$taihe_generated_file_path/src/ohos.file.storageStatistics.abi.c",
  ]
}

taihe_shared_library("storage_statistics_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  subsystem_name = "$subsystem_name"
  part_name = "$part_name"

  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  include_dirs = [
    "include",
    "//foundation/filemanagement/storage_service/interfaces/innerkits/acl/native",
    "//foundation/filemanagement/storage_service/interfaces/innerkits/storage_manager/native",
    "//foundation/filemanagement/storage_service/interfaces/kits/js/storage_manager/include",
    "${storage_service_path}/services/storage_manager/include",
    "${storage_service_path}/interfaces/innerkits/storage_manager/native",
    "${storage_service_path}/services/common/include",
  ]

  defines = [
    "STORAGE_LOG_TAG = \"StorageManager\"",
    "LOG_DOMAIN = 0xD004300",
  ]

  sources = get_target_outputs(":run_taihe")
  sources += [
    "src/ani_constructor.cpp",
    "src/ohos.file.storageStatistics.impl.cpp",
    "${storage_service_path}/services/storage_manager/kits_impl/src/storage_manager_connect.cpp",
  ]

  deps = [
    ":run_taihe",
    "${storage_interface_path}/innerkits/storage_manager/native:storage_manager_sa_proxy",
  ]

  external_deps = [
    "hilog:libhilog",
    "access_token:libaccesstoken_sdk",
    "c_utils:utils",
    "file_api:filemgmt_libhilog",
    "file_api:filemgmt_libn",
    "ipc:ipc_single",
    "napi:ace_napi",
    "safwk:system_ability_fwk",
    "samgr:samgr_proxy",
  ]
}

generate_static_abc("storage_statistics_taihe_abc") {
  base_url = "$taihe_generated_file_path"
  files = [ "$taihe_generated_file_path/@ohos.file.storageStatistics.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/storage_statistics_taihe_abc.abc"
  dependencies = [ ":run_taihe" ]
}

ohos_prebuilt_etc("storage_statistics_etc") {
  source = "$target_out_dir/storage_statistics_taihe_abc.abc"
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [
    ":storage_statistics_taihe_abc"
  ]
}

group("storage_service_storage_statistics_taihe") {
  deps = [
    ":storage_statistics_taihe",
    ":storage_statistics_etc",
  ]
}
